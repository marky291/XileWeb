//Needed for Client-Button
-	script	Uber Shop	112,{
		OnDonationClicked:
				callfunc "UberShop";
}

function	script	UberShop	{

OnDonationClicked:
	// deny spending ubers on the shop due to @security and we are not dev server
	if ($environment == 1 && blockedfromspendingubers()) {
		end;
	}

	// GET UBERS FROM SQL
	query_sql("SELECT `current_ubers` FROM `donation_ubers` WHERE `account_id`='"+getcharid(3)+"'", .@current_ubers);

	dispbottom "[XileRO]: You have " + .@current_ubers + " Donate Uber.";

while(1){
	mes "[^ff0000Uber Shop^000000]";
	specialeffect2(363, AREA);
	mes "Welcome to the Uber shop, where you can spend your ubers on items";
	mes "Please choose what type of type of items you wish to browse";


	next;
	switch(select("^00BFC8Budget Shop^000000 (1-3 Ubers):^0000ffEquipment Shop^000000 (4-27 Ubers):^ff9900Weapons Shop^000000 (1-3 Ubers):^008800Card Shop^000000 (1-5 Ubers):^FF4500Costume Shop^000000 (1-85 Ubers):^8B0000Elite Shop^000000 (7-120 Ubers)")) {


	case 1:
	// Budget Shop (1-3 Ubers) - Misc Items, Shields
		setarray $@budget_item[0],    17246, 17247, 20418, 20001, 20450, 2102, 2104, 2106, 2108, 2504, 2506;
		setarray $@budget_cost[0],       2,     2,     1,     1,     3,    2,    2,    2,    2,    2,    2;
		setarray $@budget_refine[0],     0,     0,     0,     0,     0,   10,   10,   10,   10,   10,   10;

		set .@npcname$, "^00BFC8Budget Shop^000000";
		set .@array$, "budget";
		set .@tagline$, "Affordable essentials - Misc items and shields";

		callfunc("buy_donation",.@npcname$,.@tagline$,.@array$);
		break;

	case 2:
	// Equipment Shop (4-27 Ubers) - Token Sets, Ultimate Sets
		setarray $@equipment_item[0],    20101, 20102, 20103, 20104, 20105, 20106, 20107, 3170, 3172, 3171, 3173, 3175, 3174, 3176, 3178, 3177, 3163, 3164, 3165, 3166, 3167, 3168;
		setarray $@equipment_cost[0],        5,    10,     5,    10,     5,    10,     4,    6,    6,    9,    6,    6,    9,    7,    7,   11,    3,    3,    3,    3,    3,    3;
		setarray $@equipment_refine[0],     10,    10,    10,    10,    10,    10,    10,    0,    0,    0,    0,    0,    0,    0,    0,    0,    0,    0,    0,    0,    0,    0;

		set .@npcname$, "^0000ffEquipment Shop^000000";
		set .@array$, "equipment";
		set .@tagline$, "Mid-tier gear - Token sets and Ultimate equipment sets";

		callfunc("buy_donation",.@npcname$,.@tagline$,.@array$);
		break;

	case 3:
	// Weapons Shop (1-3 Ubers) - All weapons organized by tier
		setarray $@weapons_item[0],    3150, 3151, 3152, 3153, 3154, 3155, 3156, 3157, 3158, 3159, 3160, 3161, 19118, 19119, 19120, 19121, 19122, 19123, 19124, 3150, 3151, 3152, 3153, 3154, 3155, 3156, 3157, 3158, 3159, 3160, 3161, 19118, 19119, 19120, 19121, 19122, 19123, 19124;
		setarray $@weapons_cost[0],       1,    1,    1,    1,    1,    1,    1,    1,    1,    1,    1,    1,     1,     1,     1,     1,     1,     1,     1,    3,    3,    3,    3,    3,    3,    3,    3,    3,    3,    3,    3,     3,     3,     3,     3,     3,     3,     3;
		setarray $@weapons_refine[0],      0,    0,    0,    0,    0,    0,    0,    0,    0,    0,    0,    0,     0,     0,     0,     0,     0,     0,     0,   10,   10,   10,   10,   10,   10,   10,   10,   10,   10,   10,   10,    10,    10,    10,    10,    10,    10,    10;

		set .@npcname$, "^ff9900Weapons Shop^000000";
		set .@array$, "weapons";
		set .@tagline$, "Combat gear - Basic weapons (1 uber) and elite +10 weapons (3 ubers)";

		callfunc("buy_donation",.@npcname$,.@tagline$,.@array$);
		break;

	case 4:
	// Card Shop (1-5 Ubers) - All cards organized by cost
		setarray $@cards_item[0],    4172, 4196, 4147, 4047, 4198, 4207, 4208, 4209, 4210, 4211, 4121, 4128, 4441, 4263, 4403, 4318;
		setarray $@cards_cost[0],       1,    1,    2,    2,    2,    2,    2,    2,    2,    2,    3,    3,    5,    5,    5,    5;
		setarray $@cards_refine[0],     0,    0,    0,    0,    0,    0,    0,    0,    0,    0,    0,    0,    0,    0,    0,    0;

		set .@npcname$, "^008800Card Shop^000000";
		set .@array$, "cards";
		set .@tagline$, "Power enhancement - Cards to boost your character stats and abilities";

		callfunc("buy_donation",.@npcname$,.@tagline$,.@array$);
		break;

	case 5:
	// Costume Shop (1-85 Ubers) - All costumes combined
		setarray $@costume_item[0],    20398, 19808, 19809, 19810, 19811, 19812, 19813, 20466, 20472, 20469, 20467, 20473, 20470, 20468, 20474, 20471, 31329, 31327, 31328, 19563, 19594, 19595, 31265, 31254;
		setarray $@costume_cost[0],        1,     3,     3,     3,     3,     3,     3,    10,     8,    12,    10,     8,    12,    10,     8,    12,     9,     8,    14,    60,    65,    85,    20,    10;
		setarray $@costume_refine[0],      0,     0,     0,     0,     0,     0,     0,     0,     0,     0,     0,     0,     0,     0,     0,     0,     0,     0,     0,     0,     0,     0,     0,     0;

		set .@npcname$, "^FF4500Costume Shop^000000";
		set .@array$, "costume";
		set .@tagline$, "Style and fashion - XileRO relics, designer pieces and battleground costumes";

		callfunc("buy_donation",.@npcname$,.@tagline$,.@array$);
		break;

	case 6:
	// Elite Shop (7-120 Ubers) - High-end exclusive items
		setarray $@elite_item[0],    20453, 20454, 3170, 3171, 3173, 3174, 3176, 3177;
		setarray $@elite_cost[0],      120,   120,    7,   10,    7,   10,    8,   12;
		setarray $@elite_refine[0],      0,     0,   10,   10,   10,   10,   10,   10;

		set .@npcname$, "^8B0000Elite Shop^000000";
		set .@array$, "elite";
		set .@tagline$, "Premium exclusives - Flexors and ultimate +10 headgear for top supporters";

		callfunc("buy_donation",.@npcname$,.@tagline$,.@array$);
		break;

		}
	}
}



// ---------------- FUNCTIONS DO NOT EDIT ----------------------------------
// ---------------- FUNCTIONS DO NOT EDIT ----------------------------------
// ---------------- FUNCTIONS DO NOT EDIT ----------------------------------

//                    MADE BY LEMONITE

// ---------------- FUNCTIONS DO NOT EDIT ----------------------------------
// ---------------- FUNCTIONS DO NOT EDIT ----------------------------------
// ---------------- FUNCTIONS DO NOT EDIT ----------------------------------


function	script	buy_donation	{

	set .@npcname$, "[" + getarg(0) + "]";
	set .@tagline$, getarg(1);

	//INTRO MESSAGE
	if (.@tagline$ == "get access to our extra features and functionalities"){
		mes .@npcname$;
		mes "Use these Tickets to ^FF9900" +.@tagline$+ "^000000. Tickets are tradeable but once you use them, they are ^ff0000AccountBound^000000";
	}
	else{
	mes .@npcname$;
	mes "If you have donated to XileRO already, I will give you ^FF9900" +.@tagline$+ "^000000 in exchange for your uber points!";
	}

	next;

	// GET UBERS FROM SQL
	query_sql("SELECT `username`,`current_ubers` FROM `donation_ubers` WHERE `account_id`='"+escape_sql(getcharid(3))+"'", .@username$, .@current_ubers);

	// GET AMOUNT OF ITEMS THAT THE NPC HAS
	set .@size, getarraysize(getd("$@"+getarg(2)+"_item"));

	// MAKE AN ARRAY THAT CAN BE READ BY HUMANS.
	for(set .@i, 0; .@i<.@size; .@i++){
		setarray .@item_list[.@i], getd("$@"+getarg(2)+"_item["+.@i+"]");
		setarray .@cost_list[.@i], getd("$@"+getarg(2)+"_cost["+.@i+"]");
		setarray .@refine_list[.@i], getd("$@"+getarg(2)+"_refine["+.@i+"]");
	}

	// CREATE A MENU WITH THE ITEMS.
	for(set .@i, 0; .@i<.@size; .@i++){

	// COLOR DEPENDING ON WHAT TYPE OF ITEM IT IS.
		if(getiteminfo(.@item_list[.@i],2)==6)  set .@menu$, .@menu$ + "^008800";  //CARDS - GREEN
		else if(.@refine_list[.@i]) { set .@menu$, .@menu$ + "^ff0000+" + .@refine_list[.@i] + " "; }  //+10 ITEMS - RED
		else set .@menu$, .@menu$ + "^000000";	//Normal - Black
	// CREATE MENU
		set .@menu$, .@menu$ + getitemname(.@item_list[.@i]) + " - " + .@cost_list[.@i] + " ubers^000000:";
	}

	// GO BACK OPTION FOR EASIER BROWSING.
	set .@menu$, .@menu$ + "Go Back";

	// USER TO SELECT A MENU.
	set .@select, select(.@menu$)-1;

	// GO BACK OPTION
	if(.@select == .@size) {
		return;
	}

	// VARIABLES
	set .@item, .@item_list[.@select];
	set .@cost, .@cost_list[.@select];
	set .@refine, .@refine_list[.@select];

	// ASK PLAYER IF REALLY WANTS IT.
	mes .@npcname$;

	//DISPLAY THE REFINE LEVEL IF IT HAS ONE.
	if (.@refine) { set .@itemname$, "+"+.@refine+" " + getitemname(.@item); }	//IF ITEM HAS REFINE
	else { set .@itemname$, getitemname(.@item); }								//IF ITEM HAS NO REFINE

	mes .@itemname$ + " costs ^0000ff" + .@cost + "^000000 Uber points";
	mes " ";
	atcommand "@ii "+ .@item; // show in dispbottom
	mes "<ITEM>Click to Preview Item: " + .@itemname$ + "<INFO>" + .@item + "</INFO></ITEM>"; // view in box.
	next;
	if(select("Wait, I'll think about it..:I'll take that one!")==1){
		mes .@npcname$;
		mes "Okay, please take your time! For more information, ask the Donation Guide NPC. Thanks!";
		close;
	}

	// CHECK IF PLAYER HAS THE AMOUNT NEEDED
	if(.@current_ubers < .@cost) {
		mes .@npcname$;
		mes "Wait, you need ^0000FF" + callfunc("F_InsertPlural", .@cost - .@current_ubers, "Uber") + "^000000 more to get this item!";
		mes " ";
		mes "Come back when you have it!";
		close;
	}

	// check weight first
	if (checkweight(.@item, 1) == 0)
	{
		mes .@npcname$;
		mes "Oh look, you appear to be overweight, maybe you should remove some items before I give you " + getitemname(.@item);
		close;
	}

	// GIVE PLAYER THE ITEM.
	specialeffect2 154;
	getitem2 .@item,1,1,.@refine,0,0,0,0,0;

	// DEBUG
	// dispbottom "Item : " + .@item;
	// dispbottom "Cost :  " + .@cost;
	// dispbottom "Refine : " + .@refine;

	// UPDATE MYSQL WITH NEW BALANCE.

	set .@uber_spend, .@cost;

	// Deduct from donation ubers
	set .@current_ubers, (.@current_ubers - .@cost);
	query_sql("UPDATE donation_ubers SET current_ubers = " + .@current_ubers + " WHERE  account_id = " + getcharid(3));

	// some effects for wowzers.
	specialeffect2 488;
	specialeffect2 342;
	specialeffect2 305;
	specialeffect2 102;
	specialeffect2 103;

	// LOG ITEM PURCHASE
	set .@playerName$, escape_sql(strcharinfo(0));
	set .@usersName$, escape_sql(.@username$);
	set .@itemName$, escape_sql(getitemname(.@item));

	set .@uber_balance, .@current_ubers;

	// DISPLAY BALANCE AFTER PURCHASE.
	dispbottom "Purchase Complete, You now have " + callfunc("F_InsertPlural",(.@uber_balance),"Uber") + " left on your account";

	//log transaction db
	query_sql("INSERT INTO `logs`.`donationlog` (`account_id`, `username`, `player`, `item_name`, `refine`, `amount`, `uber_spend`, `uber_balance`, `spend_type`) VALUES ('"+escape_sql(getcharid(3))+"', '"+escape_sql(.@usersName$)+"', '"+escape_sql(.@playerName$)+"', '"+.@itemName$+"', '"+.@refine+"','"+1+"', '"+.@uber_spend+"', '"+.@uber_balance+"', 'donation')");

	// MESSAGE
	mes .@npcname$;
	mes "Thank you for donating, please come again!";
	close;

}


////OLD CODE 

/*
 -	script	Donation_Function	-1,{

OnPCLoginEvent:
	//MYSQL DATA
	query_sql("SELECT pending_ubers, current_ubers FROM donation_ubers WHERE account_id="+getcharid(3), .@pending_ubers, .@current_ubers);

	if (.@pending_ubers) {

		if (checkweight(6635, 250) == 0 || MaxWeight - Weight < 1000) {
			mes "[^ff0000Message from Staff^000000]";
			mes "Just in case you recieve a milestone tier for free items. Drop off some of your items and relog to get your donation points and milestone rewards!";
			close;
		}

		mes "[^ff0000Message from Staff^000000]";
		mes "We appreciate your donation to the server, to help us fund and give back even better things";
		next;
		mes "[^ff0000Message from Staff^000000]";
		mes "We have added ^ff0000" + .@pending_ubers + " Ubers^000000 to your Account, Enjoy~~";
		//next;
		//mes "[^ff0000Message from Staff^000000]";
		//mes "Oh and one more thing!";
		//specialeffect2 363; // valentine wings heart.
		//next;
		//mes "[^ff0000Message from Staff^000000]";
		//mes "We have mailed a ^ff0000Christmas Donation Gift^000000, available for all donations throughout December!";
		specialeffect2 491; // ....
		close2;

		//VARIABLES.
		set .@balance, .@current_ubers + .@pending_ubers;

		// mail requires an array for some reason.
		//setarray .@exclusive_item[0], 643, 20232, 9325;
		//setarray .@exclusive_amnt[0],   5,     1,    1;
		//mail getcharid(0, strcharinfo(0)), "Retro Staff", "Exclusive Donation Gift", "A gift for all donations during the month of september, Please remember to feed your pet using our pet miner in prontera, enjoy~", 0, .@exclusive_item, .@exclusive_amnt;
		//message rid2name(convertpcinfo(strcharinfo(0), CPC_ACCOUNT)),"You have recieved exclusive items by mail, from the retro staff!";

		query_sql(" SELECT ubers_received, milestone_tier FROM donation_received WHERE current_year = YEAR(CURDATE()) AND current_month = MONTH(CURDATE()) AND account_id = '" + getcharid(3) + "'; ", .@db_ubers_recieved, .@db_milestone_tier);

		set .@ubers_received_this_month, (.@db_ubers_recieved+.@pending_ubers);
		set .@milestone_tier, .@db_milestone_tier;

		if (.@db_milestone_tier != 3) 
		{
			if (.@ubers_received_this_month >= 42) {
				set .@milestone_tier, 1;
				if (.@db_milestone_tier < .@milestone_tier) {
					getitem 31267,1; //Rainbow Electric Aura
					getitem 6635,30; //Blessing
				}
			}

			if (.@ubers_received_this_month >= 88) {
				set .@milestone_tier, 2;
				if (.@db_milestone_tier < .@milestone_tier) {
					getitem 31267,1; //Rainbow Electric Aura
					getitem 31269,1;  //Mythic Rainbow Wings
					getitem 6635,75;  // Blessing
				}

			}

			if (.@ubers_received_this_month >= 129) {
				set .@milestone_tier, 3;
				if (.@db_milestone_tier < .@milestone_tier) {
					getitem 31267,1; //Rainbow Electric Aura
					getitem 31269,1;  //Mythic Rainbow Wings
					getitem 31268,1;  //Food Stand
					getitem 6635,100; //Blessing
				}
			}		
		}

		query_sql("INSERT INTO donation_received (account_id, created_at, ubers_received, milestone_tier, current_month, current_year) VALUES ('" + getcharid(3) + "', NOW(), '" + .@ubers_received_this_month + "', '" + .@milestone_tier + "', MONTH(NOW()), YEAR(NOW())) ON DUPLICATE KEY UPDATE ubers_received = VALUES(ubers_received), milestone_tier = VALUES(milestone_tier); ");

		//UPDATE MYSQL.
		query_sql("UPDATE donation_ubers SET pending_ubers=0, current_ubers="+.@balance+" WHERE account_id="+getcharid(3));
		dispbottom "You now have " + callfunc("F_InsertPlural",(.@balance),"Uber") + " on your Account";
		end;
	} 
*/

///New Code 
// Script: Donation_Function
// Description: Handles donation milestones and rewards players accordingly.
// Trigger: On Player Login

-	script	Donation_Function	-1,{
    OnPCLoginEvent:
        // Define the start and end dates for the donation period
        set .@start_date, "2025-02-14 00:00:00"; // Start of the donation period
        set .@end_date, "2025-03-31 23:59:59";   // End of the donation period

        // Fetch pending and current ubers from the database for the player's account
        query_sql("SELECT pending_ubers, current_ubers FROM donation_ubers WHERE account_id = " + getcharid(3), .@pending_ubers, .@current_ubers);

        if (.@pending_ubers > 0) {

            // Check if the player's inventory has enough weight capacity for new items
            if (checkweight(6635, 250) == 0 || (MaxWeight - Weight) < 1000) {
                mes "[^ff0000Message from Staff^000000]";
                mes "Just in case you receive a milestone tier for free items, please drop off some of your items and relog to get your donation points and milestone rewards!";
                close;
            }

            // Notify the player about their donation
            mes "[^ff0000Message from Staff^000000]";
            mes "We appreciate your donation to the server, helping us fund and provide even better things!";
            next;
            mes "[^ff0000Message from Staff^000000]";
            mes "We have added ^ff0000" + .@pending_ubers + " Ubers^000000 to your Account. Enjoy~~";

            // Apply desired special effect (e.g., visual effect upon receiving donation)
            specialeffect2 491; // Example: Special effect ID 491

            // Calculate the new balance after adding pending ubers
            set .@balance, .@current_ubers + .@pending_ubers;

            // Fetch the player's total ubers received and highest milestone tier within the date range
            query_sql("SELECT SUM(ubers_received) AS total_ubers, MAX(milestone_tier) AS max_tier FROM donation_received WHERE account_id = '" + getcharid(3) + "' AND created_at BETWEEN '" + .@start_date + "' AND '" + .@end_date + "'", .@db_ubers_received, .@db_milestone_tier);

            // Handle NULL results (no donations within date range)
            if (.@db_ubers_received == NULL) set .@db_ubers_received, 0;
            if (.@db_milestone_tier == NULL) set .@db_milestone_tier, 0;

            // Update the total ubers received within the date range
            set .@ubers_received_total, (.@db_ubers_received + .@pending_ubers);

            // Determine the new milestone tier based on the updated total ubers received
            if (.@ubers_received_total >= 129) set .@milestone_tier, 3;
            else if (.@ubers_received_total >= 88) set .@milestone_tier, 2;
            else if (.@ubers_received_total >= 42) set .@milestone_tier, 1;
            else set .@milestone_tier, 0; // No tier achieved

            // Progressively award rewards for tiers not yet claimed
            if (.@milestone_tier >= 1 && .@db_milestone_tier < 1) {
                getitem 31315, 1; // DBZ Wig
                getitem 31311, 15; // Gashapon Premium Coin x15
                getitem 6635, 30; // Blacksmith Blessings x30
            }

            if (.@milestone_tier >= 2 && .@db_milestone_tier < 2) {
                getitem 31314, 1;  // DBZ Aura Electric Aura
                getitem 31311, 15;  // Gashapon Premium Coin x30 (30 - 15)
                getitem 6635, 45;  // Blacksmith Blessings x45 (75 - 30)
            }

            if (.@milestone_tier >= 3 && .@db_milestone_tier < 3) {
                getitem 31313, 1;  // DBZ Aura
                getitem 31311, 20;  // Gashapon Premium Coin x20 (50 - 20)
                getitem 6635, 25;  // Blacksmith Blessings x25 (100 - 75)
            }

            // Update the donation_received table with the new milestone tier
            query_sql("INSERT INTO donation_received (account_id, created_at, ubers_received, milestone_tier, current_month, current_year) VALUES ('" + getcharid(3) + "', NOW(), '" + .@pending_ubers + "', '" + .@milestone_tier + "', MONTH(NOW()), YEAR(NOW())) ON DUPLICATE KEY UPDATE ubers_received = ubers_received + VALUES(ubers_received), milestone_tier = GREATEST(milestone_tier, VALUES(milestone_tier)), current_month = VALUES(current_month), current_year = VALUES(current_year)");

            // Reset pending ubers and update current ubers in donation_ubers table
            query_sql("UPDATE donation_ubers SET pending_ubers = 0, current_ubers = " + .@balance + " WHERE account_id = " + getcharid(3));

            // Notify the player about their current ubers balance
            dispbottom "You now have " + callfunc("F_InsertPlural", (.@balance), "Uber") + " on your Account";

            // Terminate the script after all operations are complete
            close2;
            end;
        }



OnInit:
	bindatcmd "donation",strnpcinfo(3)+"::OnAtDonate";
	end;

OnAtDonate:
	//ONLY RUN IF ACCOUNT IS LEVEL 99
	//if (strcharinfo(0)!= "NoviceBread") end;

	// players get warped to payon instead.
	if(getmapflag( strcharinfo(3),mf_nowarp )){
		dispbottom "You can not warp from your current map";
		end;
	}

	if(getgmlevel() < 99) {
		warp "payon",146,221;
		dispbottom "You have been warped to our donate area, thank you for your support <3";
		end;
	};

	// make sure they can do donates.
	if (strcharinfo(0) != "Marky" && strcharinfo(0) != "Mambow" && strcharinfo(0) != "Mitsune") {
		warp "payon",146,221;
		dispbottom "Warped to the donation area";
		end;
	}

	mes "[Credits Awarder]";
	mes "Enter an account username";
	next;
	input .@username$;

	//GET DATA OF USER IN DONATION TABLE.
	//IF NO DATA EXISTS, CREATE DATA AND STORE IN DONATION TABLE AND TRY AGAIN.
	while(1) {
		//GET MYSQL DATA.
		query_sql("SELECT `account_id` FROM `login` Where `userid`='" + .@username$ + "'", .@account);

		if(.@account) {
			query_sql("SELECT `account_id`, `pending_ubers`, `current_ubers` FROM `donation_ubers` WHERE `account_id`='"+.@account+"'", .@donate_account, .@pending_points, .@total_points);
			//DEBUG
			mes "[^ff0000Current MYSQL Data^000000]";
			mes "User Account : " +.@account;
			mes "Username : " +.@username$;
			mes "Pending Points : " +.@pending_points;
			mes "Total Points : " +.@total_points;
			next;
			//CHECK IF USER HAS DONATED BEFORE IF NOT, MAKE AN DONATION ACCOUNT
			if (!.@donate_account) {
				mes "[Credits Awarder]";
				mes "This person has NEVER donated before. I will ^ff0000initilize a new donation account^000000 for them.";
				next;
				query_sql("INSERT INTO `donation_ubers` (`account_id`, `username`) VALUES ('"+escape_sql(.@account)+"', '"+escape_sql(.@username$)+"')");
			}
			else break;
		}
		if(!.@account) {
			mes "[Credits Awarder]";
			mes "User is invalid or does not exist, type again.";
			next;
			if(select("Yes:Cancel")==2) close;
			input .@username$;
		}
	}

	//.@account = account id
	//.@username = username
	mes "[Credits Awarder]";
	mes "^ff0000Please review information on the account in your chatbox before proceeding..^000000";
	atcommand "@accinfo "+.@username$;
	next;
	mes "[Credits Awarder]";
	mes "^ff0000Please insert amount of Donation Points to give^000000.";
	mes " ";
	// USEFULL INFORMATION SO NO NEED TO CHECK NPC EACH TIME.
	mes "$5 = ^ff99003 Uber Points!^000000";
	mes "$10 = ^ff99008 Uber Points!^000000";
	mes "$20 = ^ff990018 Uber Points!^000000";
	mes "$40 = ^ff990042 Uber Points!^000000";
	mes "$75 = ^ff990088 Uber Points!^000000";
	mes " ";
	mes "For other amounts contact an admin.";
	next;
	input .@input_points;

	//VARIABLE.
	set .@donate_points, .@pending_points + .@input_points;
	//BALANCE VARIABLE
	set .@current_balance, .@total_points + .@pending_points;
	set .@new_balance, .@donate_points + .@total_points;

	mes "[Credits Awarder]";
	mes "^ff0000Account ID^000000 : " + .@account;
	mes "^ff0000Award Credits^000000 : " + .@input_points;
	mes "^ff0000Current Balance^000000 : " + .@current_balance;
	mes "^ff0000New Balance^000000 : " + .@new_balance;
	next;
	if(select("Send Now:Cancel")==2) close;

	//UPDATE SQL FOR REWARDED POINTS.
	query_sql("UPDATE donation_ubers SET pending_ubers="+.@donate_points+" WHERE account_id="+.@account);

	mes "[Credits Awarder]";
	mes "Credits have been sent.";

	if (isloggedin(.@account)) {
		message rid2name(.@account), "You have recieved " + .@input_points + " donation ubers. Thank you for your support <3";
		message rid2name(.@account), "To recieve these points instantly, please relog your character.";
	}

	close;
}

payon,142,224,6	script	Donation Guide	113,{

	mes "[^0000ffDonation Guide^000000]";
	mes "You can get Uber Points by donating to XileRO at www.XileRO.net.";
	next;
	if(select("Show me now!:Okay, Thank you!")==1) {
		mes "[^0000ffDonation Guide^000000]";
		mes "For Donating to XileRO you will get Uber Points to spend in the";
		mes "Donation Mall. Below are the Amount you need to Donate for the Amount of Uber Points for this Server Only.";
		next;
		mes "[^0000ffDonation Guide^000000]";
		mes "$5 = ^ff99003 Uber Points!^000000";
		mes "$10 = ^ff99008 Uber Points!^000000";
		mes "$20 = ^ff990018 Uber Points!^000000";
		mes "$40 = ^ff990042 Uber Points!^000000";
		mes "$75 = ^ff990088 Uber Points!^000000";
	}
	close;
}

prontera,140,168,6	script	Donation Guide!	790,{
	mes "[Donation Seeker]";
	mes "Welcome to XileRO! We are currently accepting donations. For these donations, you can recieve items as a thank you!!";
	next;
	mes "[Donation Seeker]";
	mes "All donations will be to make the server better and better! Thank you for supporting XileRO!!";
	mes " ";
	mes "<URL>View donation guide on our wiki!<INFO>http://wiki.xilero.net/index.php?title=Donation,1024,768</INFO></URL>";
	close;
}
